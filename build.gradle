buildscript {
	repositories {
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath "com.gradle.publish:plugin-publish-plugin:0.9.7"
	}
}

apply plugin: "groovy"
apply plugin: "com.gradle.plugin-publish"
apply plugin: "maven"

sourceCompatibility = 1.7
group = "com.github.gan0803.gradle"
version = "1.0.0"

repositories {
	mavenCentral()
	jcenter()
}

dependencies {
	compile gradleApi()
	compile localGroovy()
	compile "org.ajoberstar:gradle-git:1.7.2"
}

task javadocJar(type: Jar) {
	classifier = "javadoc"
	from javadoc
}

task sourcesJar(type: Jar) {
	classifier = "sources"
	from sourceSets.main.allSource
}

artifacts {
	archives javadocJar, sourcesJar
}

task wrapper(type: Wrapper) {
	gradleVersion = "4.5.1"
}

pluginBundle {
	website = "https://github.com/Gan0803/gradle-git-repo-plugin"
	vcsUrl = "https://github.com/Gan0803/gradle-git-repo-plugin"
	description = "Use a git repo as a maven-style repo"
	tags = ["git", "maven", "repository"]

	plugins {
		greetingsPlugin {
			id = "com.github.gan0803.gradle.git-repo-plugin"
			displayName = "Gradle git repository plugin"
		}
	}
}

def repo = new File(rootDir, "releases")

uploadArchives {
	repositories {
		mavenDeployer {
			repository url: "file://${repo.absolutePath}"
			pom.version = "${version}"
			pom.groupId = "${project.group}"
			pom.artifactId = "gradle-git-repo-plugin"
		}
	}
}